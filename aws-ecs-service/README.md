# ECS Service

This module creates an ECS service fronted by a public ALB.

## Terraform managed task definition vs czecs

If the user sets `var.manage_task_definition = true`, Terraform will manage the lifecycle
of the container definition; any external changes are reset on the next Terraform run.

If var.manage_task_definition = false, the user is expected to manage the
container definition external to Terraform (e.g. using [czecs](https://github.com/chanzuckerberg/czecs)). Upon creation,
Terraform will use a stub definition, but from that point forward will ignore any
changes to the definition, allowing external task definition management.

## Example

```hcl
data "aws_route53_zone" "zone" {
  name         = "example.com."
  private_zone = false
}

module "role" {
  source    = "github.com/chanzuckerberg/cztack//aws-iam-ecs-task-role?ref=v0.21.3"
  project   = var.project
  env       = var.env
  service   = var.component
  owner     = var.owner
}

module "role-policy" {
  source    = "github.com/chanzuckerberg/cztack//aws-params-reader-policy?ref=v0.21.3"
  project   = var.project
  env       = var.env
  service   = var.component
  region    = var.region
  role_name = module.role.name
}

# This will define a task that runs this (example) container.
locals {
  template = <<TEMPLATE
[
  {
    "name": "myservice",
    "image": "chanzuckerberg/myservice:branch-master",
    "cpu": 1024,
    "memoryReservation": 1024,
    "essential": true,
    "portMappings": [
      {
        "containerPort": 80,
        "hostPort": 0        # If awsvpc_network_mode is true, must match containerPort so must be 80
      }
    ],
    "environment": [
      { "name": "AWS_REGION",           "value": "${var.region}" },
      { "name": "AWS_DEFAULT_REGION",   "value": "${var.region}" },
      { "name": "ENVIRONMENT",          "value": "${var.env}" },
    ],
    "logConfiguration": {
      "logDriver": "awslogs",
      "options": {
        "awslogs-group": "${var.project}-${var.env}-myservice",
        "awslogs-region": "${var.region}",
        "awslogs-stream-prefix": "${var.container_name}"
      }
    }
  }
]
TEMPLATE
}

data "aws_acm_certificate" "staging" {
  domain   = var.hostname
  statuses = ["ISSUED"]
}

module "web-service" {
  source = "github.com/chanzuckerberg/cztack//aws-ecs-service?ref=v0.21.3"

  # this is the name of the service and many of the resources will have this name
  service = "myservice"

  # The number of tasks to run (no autoscaling currently)
  desired_count       = 2

  # These two together will set the domain name for your service.
  subdomain           = "myservice"
  route53_zone_id     = data.aws_route53_zone.zone.id

  # Parameters used for naming and tagging, auto-generated by fogg if you are using it.
  project = var.project
  owner   = var.owner
  env     = var.env

  # This port will exposed on this named container. This named container will also be
  # the one to which the load balancer points. The cert will be attached to the https listener.
  # The health of that contiainer will be checked with the `health_check_path` path.
  container_port      = 80
  container_name      = myservice
  acm_certificate_arn = data.aws_acm_certificate.staging.arn
  health_check_path   = "/health"

  # The VPC and subnets in which the load balancer will be created.
  vpc_id              = data.terraform_remote_state.cloud-env.outputs.vpc_id
  lb_subnets          = data.terraform_remote_state.cloud-env.outputs.public_subnets

  # The cluster in which the service is run.
  cluster_id          = data.terraform_remote_state.ecs.outputs.cluster_id
  # See above. This is what defines the containers that are run.
  task_definition     = local.template

  # The task is given this role. Useful for services that need to make API calls to AWS.
  task_role_arn       = module.role.arn

  with_service_discovery = true
}
```

## Service Discovery

In addition to a load balancer, this module supports registering all instances of
tasks with DNS via ECS service discovery. If with_service_discovery is true, a new private
DNS zone is created, and the tasks are registered in that DNS zone. The domain name is only
resolvable from within the VPC; it is not publicly resolvable.

## Migrating old ECS services
Older ECS services were created with an ARN in an old format that did not include the ECS cluster name as part of the ARN. AWS began allowing opt-in to the new ARN format starting November 15, 2018, and will require the new format starting January 1, 2020. ECS only allows applying tags (such as cost tags) on services that have the new ARN format. Applying tags to older ECS services using the old ARN format will return the following error message:
```
InvalidParameterException: Long arn format must be used for tagging operations
```
This module by default will assume your organization has opted in to the new ARN format and will apply tags to the ECS service. Creating new services after the opt-in will work fine, but migrating an existing older ECS service to using this module (via a state mv or an import) will encounter the above error message the next time it is applied.

Since changing a service to use the new ARN requires destroying and recreating the service, this can result in downtime. In such cases, you can opt-out applying tags by passing `tag_service = false` as an argument to the module. It is recommended that at the next possible down time, the ECS service be replaced by running `terraform taint`, and if `manage_task_definition = false` restoring the ECS task definition version (the taint/replace will restore to only the last stub definition). After the service is destroy/replaced, the `tag_
service = false` argument can be removed.

<!-- START -->
## Requirements

No requirements.

## Providers

| Name | Version |
|------|---------|
| aws | n/a |

## Inputs

| Name | Description | Type | Default | Required |
|------|-------------|------|---------|:--------:|
| access\_logs\_bucket | S3 bucket to write alb access logs to. Null for no access logs. | `string` | `null` | no |
| acm\_certificate\_arn | Certificate for the HTTPS listener. | `string` | n/a | yes |
| awsvpc\_network\_mode | Give the task its own IP and security group if true. Use host EC2 network if false. | `bool` | `false` | no |
| cluster\_id | n/a | `string` | n/a | yes |
| container\_name | Name of the container. Must match name in task definition. If omitted, defaults to name derived from project/env/service. | `string` | `null` | no |
| container\_port | The port the container to be exposed to is listening on. | `number` | n/a | yes |
| desired\_count | n/a | `number` | n/a | yes |
| disable\_http\_redirect | Disable redirecting HTTP to HTTPS. | `bool` | `true` | no |
| env | Env for tagging and naming. See [doc](../README.md#consistent-tagging). | `string` | n/a | yes |
| extra\_tags | Extra tags that will be added to components created by this module. | `map(string)` | `{}` | no |
| health\_check\_grace\_period\_seconds | Seconds to ignore failing load balancer health checks on newly instantiated tasks to prevent premature shutdown, up to 7200. | `number` | `60` | no |
| health\_check\_interval | Time between health checks of the underlying service. | `number` | `null` | no |
| health\_check\_matcher | Range of HTTP status codes considered success for health checks. [Doc](https://www.terraform.io/docs/providers/aws/r/lb_target_group.html#matcher) | `string` | `"200-399"` | no |
| health\_check\_path | n/a | `string` | `"/"` | no |
| health\_check\_timeout | Timeout for a health check of the underlying service. | `number` | `null` | no |
| internal\_lb | n/a | `bool` | `false` | no |
| lb\_idle\_timeout\_seconds | n/a | `number` | `60` | no |
| lb\_ingress\_cidrs | n/a | `list(string)` | <pre>[<br>  "0.0.0.0/0"<br>]</pre> | no |
| lb\_ingress\_security\_group\_ids | n/a | `list(string)` | `[]` | no |
| lb\_subnets | List of subnets in which to deploy the load balancer. | `list(string)` | n/a | yes |
| manage\_task\_definition | If false, Terraform will not touch the task definition for the ECS service after initial creation | `bool` | `true` | no |
| ordered\_placement\_strategy | Placement strategy for the task instances. | `list(object({ type = string, field = string }))` | `[]` | no |
| owner | Owner for tagging and naming. See [doc](../README.md#consistent-tagging). | `string` | n/a | yes |
| project | Project for tagging and naming. See [doc](../README.md#consistent-tagging) | `string` | n/a | yes |
| registry\_secretsmanager\_arn | ARN for AWS Secrets Manager secret for credentials to private registry | `string` | `null` | no |
| route53\_zone\_id | Zone in which to create an alias record to the ALB. | `string` | n/a | yes |
| service | Service for tagging and naming. See [doc](../README.md#consistent-tagging). | `string` | n/a | yes |
| slow\_start | Seconds for targets to warm up before the load balancer sends them a full share of requests. 30-900 seconds or 0 to disable. | `number` | `60` | no |
| ssl\_policy | ELB policy to determine which SSL/TLS encryption protocols are enabled. Probably don't touch this. | `string` | `null` | no |
| subdomain | Subdomain in the zone. Final domain name will be subdomain.zone | `string` | n/a | yes |
| tag\_service | Apply cost tags to the ECS service. Only specify false for backwards compatibility with old ECS services. | `bool` | `true` | no |
| task\_definition | JSON to describe task. If omitted, defaults to a stub task that is expected to be managed outside of Terraform. | `string` | `null` | no |
| task\_egress\_cidrs | CIDR blocks the task is allowed to communicate with for outbound traffic. Only used if awsvpc\_network\_mode is true. | `list(string)` | <pre>[<br>  "0.0.0.0/0"<br>]</pre> | no |
| task\_egress\_security\_group\_ids | Security groups the task is allowed to communicate with for outbound traffic. Only used if awsvpc\_network\_mode is true. | `list(string)` | `[]` | no |
| task\_role\_arn | n/a | `string` | n/a | yes |
| task\_subnets | List of subnets in which to deploy the task for awsvpc networking mode. Only used if awsvpc\_network\_mode is true. | `list(string)` | `[]` | no |
| vpc\_id | n/a | `string` | n/a | yes |
| with\_service\_discovery | Register the service with ECS service discovery. Adds a sub-zone to the given route53\_zone\_id. | `bool` | `false` | no |

## Outputs

| Name | Description |
|------|-------------|
| alb\_access\_logs\_prefix | ALB access logs S3 prefix |
| alb\_dns\_name | n/a |
| alb\_http\_listener\_arn | ALB HTTP listener ARN, only if HTTPS forwarding is disabled |
| alb\_https\_listener\_arn | ALB HTTPS listener ARN |
| alb\_route53\_zone\_id | n/a |
| container\_security\_group\_id | Security group id for the container. |
| ecs\_task\_definition\_family | The family of the task definition defined for the given/generated container definition. |
| private\_service\_discovery\_domain | Domain name for service discovery, if with\_service\_discovery=true. Only resolvable within the VPC. |

<!-- END -->
