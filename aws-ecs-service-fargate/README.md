# ECS Fargate Service

This module creates an ECS Fargate service fronted by a public ALB.

## Notes

In Fargate, the hostPort and containerPort must match; 0 is not allowed for hostPort, and omitting hostPort is not allowed.

## Terraform managed task definition vs czecs

If the user sets `var.manage_task_definition = true`, Terraform will manage the lifecycle
of the container definition; any external changes are reset on the next Terraform run.

If var.manage_task_definition = false, the user is expected to manage the
container definition external to Terraform (e.g. using [czecs](https://github.com/chanzuckerberg/czecs)). Upon creation,
Terraform will use a stub definition, but from that point forward will ignore any
changes to the definition, allowing external task definition management.

## Example

```hcl
data "aws_route53_zone" "zone" {
  name         = "example.com."
  private_zone = false
}

data "aws_iam_policy_document" "assume_role" {
  statement {
    principals {
      type        = "Service"
      identifiers = ["ecs-tasks.amazonaws.com"]
    }

    actions = ["sts:AssumeRole"]
  }
}

resource "aws_iam_role" "role" {
  name               = "${var.project}-${var.env}-myservice"
  description        = "Task role for myservice in ${var.env} environment"
  assume_role_policy = data.aws_iam_policy_document.assume_role.json
}

module "role-policy" {
  source    = "github.com/chanzuckerberg/cztack//aws-params-reader-policy?ref=v0.19.4"
  project   = var.project
  env       = var.env
  service   = var.component
  region    = var.region
  role_name = aws_iam_role.role.name
}

# This will define a task that runs this (example) container.
locals {
  template = <<TEMPLATE
[
  {
    "name": "myservice",
    "image": "chanzuckerberg/myservice:branch-master",
    "cpu": 1024,
    "memoryReservation": 1024,
    "essential": true,
    "portMappings": [
      {
        "containerPort": 80,
        "hostPort": 80       # Must match containerPort in Fargate
      }
    ],
    "environment": [
      { "name": "AWS_REGION",           "value": "${var.region}" },
      { "name": "AWS_DEFAULT_REGION",   "value": "${var.region}" },
      { "name": "ENVIRONMENT",          "value": "${var.env}" },
    ],
    "logConfiguration": {
      "logDriver": "awslogs",
      "options": {
        "awslogs-group": "${var.project}-${var.env}-myservice",
        "awslogs-region": "${var.region}",
        "awslogs-stream-prefix": "${var.container_name}"
      }
    }
  }
]
TEMPLATE
}

data "aws_acm_certificate" "staging" {
  domain   = var.hostname
  statuses = ["ISSUED"]
}

module "web-service" {
  source = "github.com/chanzuckerberg/cztack//aws-ecs-service-fargate?ref=v0.20.0"

  # this is the name of the service and many of the resources will have this name
  service = "myservice"

  # The number of tasks to run (no autoscaling currently)
  desired_count       = 2

  # These two together will set the domain name for your service.
  subdomain           = "myservice"
  route53_zone_id     = data.aws_route53_zone.zone.id

  # Parameters used for naming and tagging, auto-generated by fogg if you are using it.
  project = var.project
  owner   = var.owner
  env     = var.env

  # This port will exposed on this named container. This named container will also be
  # the one to which the load balancer points. The cert will be attached to the https listener.
  # The health of that contiainer will be checked with the `health_check_path` path.
  container_port      = 80
  container_name      = myservice
  acm_certificate_arn = data.aws_acm_certificate.staging.arn
  health_check_path   = "/health"

  # The VPC and subnets in which the load balancer will be created.
  vpc_id              = data.terraform_remote_state.cloud-env.outputs.vpc_id
  lb_subnets          = data.terraform_remote_state.cloud-env.outputs.public_subnets
  task_subnets        = data.terraform_remote_state.cloud-env.outputs.private_subnets

  # The cluster in which the service is run.
  cluster_id          = data.terraform_remote_state.ecs.outputs.cluster_id
  # See above. This is what defines the containers that are run.
  task_definition     = local.template

  # The task is given this role. Useful for services that need to make API calls to AWS.
  task_role_arn       = aws_iam_role.role.arn

  cpu                 = 256
  memory              = 512

  with_service_discovery = true
}
```

## Service Discovery

In addition to a load balancer, this module supports registering all instances of
tasks with DNS via ECS service discovery. If with_service_discovery is true, a new private
DNS zone is created, and the tasks are registered in that DNS zone. The domain name is only
resolvable from within the VPC; it is not publicly resolvable.

<!-- START -->
## Inputs

| Name | Description | Type | Default | Required |
|------|-------------|:----:|:-----:|:-----:|
| access\_logs\_bucket | S3 bucket to write alb access logs to. Empty for no access logs. | string | `null` | no |
| acm\_certificate\_arn | Certificate for the HTTPS listener. | string | n/a | yes |
| cluster\_id |  | string | n/a | yes |
| container\_egress\_cidrs | CIDR blocks the task is allowed to communicate with for outbound traffic. | list | `["0.0.0.0/0"]` | no |
| container\_egress\_security\_group\_ids | Security groups the task is allowed to communicate with for outbound traffic. | list | `[]` | no |
| container\_name | Name of the container. Must match name in task definition. If omitted, defaults to name derived from project/env/service. | string | `null` | no |
| container\_port |  | number | n/a | yes |
| cpu | CPU units for Fargate task. Used if var.manage_task_definition provided, or for initial stub task if externally managed. | number | `256` | no |
| desired\_count |  | number | n/a | yes |
| disable\_http\_redirect | Disable redirecting HTTP to HTTPS. | bool | `true` | no |
| env | Env for tagging and naming. See [doc](../README.md#consistent-tagging). | string | n/a | yes |
| extra\_tags | Extra tags that will be added to components created by this module. | map | `{}` | no |
| health\_check\_grace\_period\_seconds | Seconds to ignore failing load balancer health checks on newly instantiated tasks to prevent premature shutdown, up to 7200. | number | `60` | no |
| health\_check\_matcher | Range of HTTP status codes considered success for health checks. [Doc](https://www.terraform.io/docs/providers/aws/r/lb_target_group.html#matcher) | string | `"200-399"` | no |
| health\_check\_path |  | string | `"/"` | no |
| internal\_lb |  | bool | `false` | no |
| lb\_idle\_timeout\_seconds |  | number | `60` | no |
| lb\_ingress\_cidrs |  | list | `<list>` | no |
| lb\_ingress\_security\_group\_ids |  | list | `<list>` | no |
| lb\_subnets | List of subnets in which to deploy the load balancer. | list | n/a | yes |
| manage\_task\_definition | If false, Terraform will not touch the task definition for the ECS service after initial creation | bool | `true` | no |
| memory | Memory in megabytes for Fargate task. Used if task_definition provided, or for initial stub task if externally managed. | number | `512` | no |
| owner | Owner for tagging and naming. See [doc](../README.md#consistent-tagging). | string | n/a | yes |
| project | Project for tagging and naming. See [doc](../README.md#consistent-tagging) | string | n/a | yes |
| registry\_secretsmanager\_arn | ARN for AWS Secrets Manager secret for credentials to private registry | string | `null` | no |
| route53\_zone\_id | Zone in which to create an alias record to the ALB. | string | n/a | yes |
| service | Service for tagging and naming. See [doc](../README.md#consistent-tagging). | string | n/a | yes |
| ssl\_policy | ELB policy to determine which SSL/TLS encryption protocols are enabled. Probably don't touch this. | string | `null` | no |
| subdomain | Subdomain in the zone. Final domain name will be subdomain.zone | string | n/a | yes |
| tag\_service | Apply cost tags to the ECS service. Only specify false for backwards compatibility with old ECS services. | bool | `true` | no |
| task\_definition | JSON to describe task. If omitted, defaults to a stub task that is expected to be managed outside of Terraform. | string | `null` | no |
| task\_role\_arn |  | string | n/a | yes |
| task\_subnets | List of subnets in which to deploy the task for awsvpc networking mode. | list | `[]` | no |
| vpc\_id |  | string | n/a | yes |
| with\_service\_discovery | Register the service with ECS service discovery. Adds a sub-zone to the given route53_zone_id. | bool | `false` | no |

## Outputs

| Name | Description |
|------|-------------|
| alb\_access\_logs\_prefix | ALB access logs S3 prefix |
| alb\_dns\_name |  |
| alb\_route53\_zone\_id |  |
| container\_security\_group\_id | Security group id for the container. |
| ecs\_task\_definition\_family | The family of the task definition defined for the given/generated container definition. |
| private\_service\_discovery\_domain | Domain name for service discovery, if with_service_discovery=true. Only resolvable within the VPC. |

<!-- END -->
